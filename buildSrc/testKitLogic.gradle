test {
    // test project under build, but nuke it first to be sure it's clean
    def testProjectDir = "$buildDir/testProject"
    systemProperty 'testProjectDir', testProjectDir
    doFirst {
        delete testProjectDir
    }
    // path to shared testkit gradle home
    systemProperty 'testKitDir', file("$rootProject.buildDir/.gradle")
    // dynamic marytts version
    systemProperty 'maryttsVersion', maryttsVersion
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")
    ext.outputFile = file("$outputDir/plugin-classpath.txt")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        outputFile.text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testCompile gradleTestKit()
    testRuntime files(createClasspathManifest)
}
